#version 430

layout(local_size_x = 8, local_size_y = 8, local_size_z = 8) in;

layout(binding = 0, rgba8) uniform readonly image3D src;
layout(binding = 1, rgba8) uniform writeonly image3D dst;

void main() {
    ivec3 threadId = ivec3(gl_GlobalInvocationID.xyz);
    ivec3 dstSize = imageSize(dst);

    if (any(greaterThan(threadId, dstSize)))
        return;

    ivec3 srcCoord = 2 * threadId;

    const ivec3 offsets[] = ivec3[](
        ivec3(0, 0, 0), ivec3(0, 0, 1), ivec3(0, 1, 0), ivec3(0, 1, 1),
        ivec3(1, 0, 0), ivec3(1, 0, 1), ivec3(1, 1, 0), ivec3(1, 1, 1)
    );

    vec4 value = vec4(0);
    for (int i = 0; i < 8; i++) {
        value += imageLoad(src, srcCoord + offsets[i]);
    }
    value *= 0.125;

    imageStore(dst, threadId, value);
}
